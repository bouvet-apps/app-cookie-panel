plugins {
    id 'java'
    id "com.enonic.xp.app" version "3.2.0"
    id 'com.github.node-gradle.node' version '3.2.1'
    id "maven-publish"
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = "${group}"
            artifactId = "${projectName}"
            version = "${version}"

            from components.java
        }
    }
}

app {
    name = project.ext.appName
    displayName = project.ext.displayName
    vendorName = project.ext.vendorName
    vendorUrl = project.ext.vendorUrl
    systemVersion = xpVersion
}

group = 'com.github.bouvet-apps'

compileJava.options.encoding = "UTF-8"

task npmBuild(type: NpmTask,
        group: 'node',
        dependsOn: 'npmInstall',
        description: 'Transpile ES6 files'
) {
    args = ['run', 'build']

    inputs.dir 'src/main/resources'
    outputs.dir "$buildDir/resources/main"
}
rootProject.tasks.getByName("processResources").dependsOn npmBuild

task frontend {
    dependsOn(':frontend:build')
    inputs.dir 'src/frontend/'
    outputs.dir "$buildDir/resources/main"
}
rootProject.tasks.getByName("processResources").dependsOn frontend

task deployApp {
    dependsOn(':deploy')
    group 'application'
    description 'Builds server-side components and deploys application'
    doLast {
        copy {
            from "build/libs/${projectName}.jar"
            into "dist"
        }
    }
}

dependencies {
    implementation "com.enonic.xp:core-api:${xpVersion}"
    implementation "com.enonic.xp:portal-api:${xpVersion}"
    include "com.enonic.xp:lib-portal:${xpVersion}"
    include "com.enonic.xp:lib-context:${xpVersion}"
    include "com.enonic.xp:lib-event:${xpVersion}"
    include 'com.enonic.lib:lib-cache:2.2.0'
}

repositories {
    mavenLocal()
    jcenter()
    xp.enonicRepo()
    maven { url 'https://repo.enonic.com/public' }
    maven { url 'https://jitpack.io' }
}

node {
    version = '16.16.0'
    npmVersion = '8.11.0'
    download = true
}

wrapper {
    gradleVersion = '8.3'
    distributionType = Wrapper.DistributionType.BIN
}
